import java.time.LocalDate

plugins {
    id 'application'
    id 'distribution'
    id 'java'

    id 'com.github.gmazzo.buildconfig' version "3.0.3"
    id 'org.openjfx.javafxplugin' version '0.0.11'
    id "org.panteleyev.jpackageplugin" version "1.3.1"
    id "org.moditect.gradleplugin" version "1.0.0-rc3" // FIXME Creates circular dependency with buildconfig
}

group 'bayern.steinbrecher'
version '0.1'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

sourceCompatibility = 17
targetCompatibility = 17

test {
    useTestNG()
}

dependencies {
    implementation "bayern.steinbrecher:CheckedElements:0.13-rc.10-SNAPSHOT"
    implementation "bayern.steinbrecher:JavaUtility:0.18"
    implementation 'bayern.steinbrecher:ScreenSwitcher:0.2.4'
    implementation 'com.itextpdf:kernel:7.1.17'
    implementation 'com.itextpdf:io:7.1.17'
    implementation 'com.itextpdf:layout:7.1.17'
    implementation 'org.jetbrains:annotations:22.0.0'

    implementation 'org.testng:testng:7.5' // FIXME Using testImplementation does not work
}

javafx {
    version = "$sourceCompatibility"
    modules = [
            "javafx.controls",
            "javafx.fxml",
            "javafx.graphics",
            "javafx.swing"
    ]
}

application {
    applicationDefaultJvmArgs = [
            /* 2021-03-07: Required for determining the pagination child that contains the current page content in
             * SnapshotPagination
             */
            '--add-opens', "javafx.controls/javafx.scene.control.skin=$moduleName" as String
    ]
}

run {
    main = "$moduleName/bayern.steinbrecher.woodpacker.WoodPacker"
}

distributions {
    main {
        contents {
            from("${projectDir}/licenses") { into "bin/licenses" }
        }
    }
}

buildConfig {
    buildConfigField("String", "APP_NAME", "\"${project.name}\"")
    buildConfigField("String", "APP_VERSION", "\"${project.version}\"")
    buildConfigField("String", "APP_VERSION_NICKNAME", "\"Das Alpha-Tier\"")
    buildConfigField("long", "BUILD_TIME", "${System.currentTimeMillis()}L")
    packageName("bayern.steinbrecher.woodpacker")
}

tasks.register("copyInstallerDeps", Copy) {
    from(configurations.runtimeClasspath)
    into("$buildDir/installer/files")
}

tasks.register("copyInstallerJars", Copy) {
    from(tasks.jar)
    into("$buildDir/installer/files")
}

tasks.jpackage {
    dependsOn("build", "copyInstallerDeps", "copyInstallerJars")

    appName = "\"${project.name}\""
    appVersion = "\"${project.version}\""
    vendor = "StoneSoft"
    copyright = "Copyright (c) ${LocalDate.now().getYear()} $vendor"
    module = "$run.main"
    modulePaths = file("$buildDir/installer/files").listFiles() as List<String>
    destination = "$buildDir/installer"

    windows {
        icon = "imageTemplates/logo.ico"
        winDirChooser = true
        winMenu = true
    }
}
