plugins {
    id 'application'
    id 'distribution'
    id 'java'

    id 'com.github.gmazzo.buildconfig' version "2.0.2"
    id 'org.openjfx.javafxplugin' version '0.0.8' // NOTE 0.0.9 may not work with the current Gradle version
}

group 'bayern.steinbrecher'
version '0.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

sourceCompatibility = 15
targetCompatibility = 15

test {
    useTestNG()
}

dependencies {
    implementation "bayern.steinbrecher:CheckedElements:0.12-rc.1-SNAPSHOT"
    implementation "bayern.steinbrecher:JavaUtility:0.18-rc.4-SNAPSHOT"
    implementation "bayern.steinbrecher:ScreenSwitcher:0.2.1-SNAPSHOT"
    implementation "com.itextpdf:kernel:7.1.14"
    implementation "com.itextpdf:io:7.1.14"
    implementation "com.itextpdf:layout:7.1.14"
    implementation 'org.jetbrains:annotations:20.1.0'

    implementation "org.testng:testng:7.3.0" // FIXME Using testImplementation does not work
}

run {
    main = "$moduleName/bayern.steinbrecher.woodpacker.WoodPacker"
}

distributions {
    main {
        contents {
            from("${projectDir}/licenses") {into "bin/licenses"}
        }
    }
}

buildConfig {
    buildConfigField("String", "APP_NAME", "\"${project.name}\"")
    buildConfigField("String", "APP_VERSION", "\"${project.version}\"")
    buildConfigField("String", "APP_VERSION_NICKNAME", "\"Das Alpha-Tier\"")
    buildConfigField("long", "BUILD_TIME", "${System.currentTimeMillis()}L")
    packageName("bayern.steinbrecher.woodpacker")
}

javafx {
    version = "$sourceCompatibility"
    modules = [
            "javafx.controls",
            "javafx.fxml",
            "javafx.graphics",
            "javafx.swing"
    ]
}
